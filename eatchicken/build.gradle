apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def rootPath = getRootDir().absolutePath
def cName = getName()

def modelConfig = pluginConfigs[cName]

println("plugin-config: $modelConfig")

android {
    compileSdkVersion 26

    signingConfigs {
        release {
            // 签名
            keyAlias signingRelease.keyAlias
            keyPassword signingRelease.keyPassword
            storeFile signingRelease.storeFile
            storePassword signingRelease.storePassword
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    aaptOptions {
        cruncherEnabled = false
    }

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        applicationId modelConfig["pluginPackageName"]
        versionCode modelConfig["pluginVersion"]
        versionName modelConfig["pluginVersionName"]
        buildConfigField "boolean", "isPlugin", "$isPlugin"

        manifestPlaceholders = [
                pluginVer: modelConfig["pluginVer"],
        ]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation repluginplugin
    implementation deps.kotlin.stdlib
    implementation deps.support.constraint_layout
    implementation deps.support.design
    implementation deps.support.v4
    implementation deps.support.app_compat
    implementation deps.glide
    implementation deps.gson
    implementation deps.okhttp
    implementation deps.retrofit
    implementation deps.rxandroid
    implementation deps.rxjava
    implementation deps.banner
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
}

def pluginApk = new File("$rootDir/eatchicken/build/outputs/apk/debug/eatchicken-debug.apk")
if (pluginApk.exists()) {
    def pluginJar = new File("$rootDir/host/src/main/assets/plugins/eatchiken-debug.jar")
    if (pluginJar.exists()) {
        pluginJar.delete()
    }
    GFileUtils.copyFile(pluginApk, pluginJar)
}

if (isPlugin) {
    // 这个plugin需要放在android配置之后，因为需要读取android中的配置项
    apply plugin: 'replugin-plugin-gradle'
    repluginPluginConfig {
        pluginName = modelConfig["pluginPackageName"]
        hostApplicationId = "co.tiangongsky.bxsdkdemo"
        hostAppLauncherActivity = "co.tiangongsky.bxsdkdemo.ui.start.StartActivity"
    }

    // debug

    def extName = '.jar'
    def folder = 'plugins'
    if (!modelConfig['pliginIsInner']) {
        extName = '.apk'
        folder = 'external'
    }
    task "buildDebug${cName}JarToHost"(type: Copy, dependsOn: ['assembleDebug']) {
        group 'abuildjartohost'
        from("$rootPath/$cName/build/outputs/apk/debug/$cName-debug.apk") //从这个目录下取出默认apk包
        into("$rootPath/host/src/main/assets/$folder/") //将jar包输出到指定目录下
        rename("$cName-debug.apk", "$cName-debug$extName") //自定义jar包的名字
    }

    task buildDebugJarToHost(dependsOn: "buildDebug${cName}JarToHost") {
        group 'abuildjartohost'
    }

    task "buildRelease${cName}JarToHost"(type: Copy, dependsOn: ['assembleRelease']) {
        group 'abuildjartohost'
        from("$rootPath/$cName/build/outputs/apk/release/$cName-release.apk") //从这个目录下取出默认apk包
        into("$rootPath/host/src/main/assets/$folder/") //将jar包输出到指定目录下
        rename("$cName-release.apk", "${modelConfig["pluginPackageName"]}$extName") //自定义jar包的名字
    }

    task buildReleaseJarToHost(dependsOn: "buildRelease${cName}JarToHost") {
        group 'abuildjartohost'
    }

    task "buildRelease${cName}ApkProduce"(type: Copy, dependsOn: ['assembleRelease']) {
        group 'abuildproduceapk'
        from("$rootPath/$cName/build/outputs/apk/release/$cName-release.apk") //从这个目录下取出默认apk包
        into("$rootPath/apk/plugins") //将jar包输出到指定目录下
        rename("$cName-release.apk", "${modelConfig["pluginPackageName"]}_${modelConfig["pluginVersion"]}.apk")
        //自定义jar包的名字
    }

    task buildReleaseApkProduce(dependsOn: "buildRelease${cName}ApkProduce") {
        group 'abuildproduceapk'
    }

    // 导入 移除的模块
    apply from: "../host-plugin-config/remoceLabelIcon.gradle"

    afterEvaluate {
        tasks.matching {
            // processDebugManifest
            it.name.startsWith('process') && (it.name.endsWith('ReleaseResources') || it.name.endsWith('DebugResources'))
        }.each { tk ->
            if (tk.name.contains("DebugResources"))
                tk.dependsOn(removeDebugLabelIcon)
            else if (tk.name.contains("ReleaseResources"))
                tk.dependsOn(removeReleaseLabelIcon)
        }
    }
}